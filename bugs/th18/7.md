[title=UM: Dragon Eater's Cataclysm]
# Dragon Eater's Cataclysm
[hr_major]

## [specs]
[hr_minor]

* **Versions**: [yes]1.00a[/yes]
* **Difficulty**: [no]Easy[/no] - [no]Normal[/no] - [no]Hard[/no] - [no]Lunatic[/no] - [yes]Extra[/yes]
* **Mode**: [yes]Main game[/yes] - [no]Practice mode[/no] - [yes]Spell practice[/yes]
* **Shottype**: [yes]Reimu[/yes] - [yes]Marisa[/yes] - [yes]Sakuya[/yes] - [yes]Sanae[/yes]
* **Cards**: None

## [what]
[hr_minor]

Momoyo's 8th spell-card can freak out and spawn seemingly an infinite number of bullets.

[img=bugs/th18/7_spam.png, hratio=2, other= ] [br]

## [how]
[hr_minor]

There are two requirements to be met for this bug to be triggered:
1. The player must time down Momoyo's 8th spell-card for at least 60 seconds.
2. The player must **NOT** be at an incredibly precise angle relative to Momoyo.

## [why]
[hr_minor]

The information provided in this section is obtained by decompiling the ``st07bs.ecl`` file through using ``thecl`` with a premade eclmap. In the following section, all instruction names will be explained. Furthermore, if you want to read more about what the instructions do, you can search it up in [a=https://priw8.github.io/#s=modding/ins&table=18]the following page[/a].

### Momoyo's subroutines
[hr_minor]

Momoyo's 8th spell-card uses several [hl2]subroutines[/hl2] to do her attacks. A subroutine is a set of instructions designed to perform a frequently used operation within a program. The following relevant subroutines to Momoyo's 8th spell-card are as follows:

[code]void BossCard8() {
    [...]
    floatTime(0, %F1, 3600, 0, 3.0f, 30.0f);
    loop {
        $I1 = _S(%F1);
        @BossCard8_at();
    }
}[/code]

[code]void BossCard8_at() {
    var ANGLE;
    [...]
    %ANGLE = %ANGLE_PLAYER - 3.1415927f;
    circlePos(%F0, %F1, %ANGLE + 1.0471976f, 1400.0f);
    enmCreate("BossCard8_et", 0.0f, 0.0f, 100, 0, 0);
    circlePos(%F0, %F1, %ANGLE + 0.5235988f, 1400.0f);
    enmCreate("BossCard8_et", 0.0f, 0.0f, 100, 0, 0);
    circlePos(%F0, %F1, %ANGLE, 1400.0f);
    enmCreate("BossCard8_et", 0.0f, 0.0f, 100, 0, 0);
    circlePos(%F0, %F1, %ANGLE - 0.5235988f, 1400.0f);
    enmCreate("BossCard8_et", 0.0f, 0.0f, 100, 0, 0);
    circlePos(%F0, %F1, %ANGLE - 1.0471976f, 1400.0f);
    enmCreate("BossCard8_et", 0.0f, 0.0f, 100, 0, 0);
    return;
}[/code]

[code]void BossCard8_et() {
    var DECREMENT;
    [...]
    $DECREMENT = $I1;
BossCard8_et_1040:
    etAngle(1, %A, 0.0f);
    %A = %A + 3.883222f;
    validRad(%A);
    etDist(1, %RANDF2 * _f(32));
    etOn(1);
    wait(3);
BossCard8_et_1284:
    if ($DECREMENT--) goto BossCard8_et_1040 @ 0;
    delete();
}
[/code]

Everything is explained in the following section. Also, [hl2]the important instructions will be highlighted[/hl2] !

### Code Explanation
[hr_minor]

+ ``BossCard8`` is what Momoyo is currently. 
+ ``BossCard8_at`` is what Momoyo attacks with. In this case, it only spawns an enemy called ``BossCard8_et``
+ ``BossCard8_et`` is the invisible enemy that is spawned by ``BossCard8_at``. ``BossCard8_et`` is the one that spawns bullets. ``BossCard8_et`` despawns after a certain time.

#### BossCard8
[hr_minor]

For convenience, the code is posted below again:

[code]void BossCard8() {
    [...]
    floatTime(0, %F1, 3600, 0, 3.0f, 30.0f);
    loop {
        $I1 = _S(%F1);
        @BossCard8_at();
    }
}[/code]

Instruction ``floatTime(int slot, float var, int time, int mode, float init, float final)`` does the following:
In ``time`` frames using ``mode`` [a=https://exphp.github.io/thpages/#/anm/interpolation]mode[/a], variable ``var`` changes from ``init`` to ``final``.
[hl2]Instruction ``floatTime(0, %F1, 3600, 0, 3.0f, 30.0f)`` does the following[/hl2]: The float variable ``F1`` is defined and updated every frame for 3600 frames long. Its initial value is 3.0 and its final value is 30.0. This takes a total of 3600 frames and it is done linearly (so on frame ``t`` the variable ``F1`` is equal to ``27/3600*t+3``).

Line ``$I1 = _S(%F1);`` declares the integer variable $I1, which is equal to the float value of ``F1`` but converted to an integer and rounded down. This is looped.

Line ``@BossCard8_at();`` calls for the subroutine ``BossCard8_at``. This is looped.

#### BossCard8_at
[hr_minor]

For convenience, the code is posted below again:

[code]void BossCard8_at() {
    var ANGLE;
    [...]
    %ANGLE = %ANGLE_PLAYER - 3.1415927f;
    circlePos(%F0, %F1, %ANGLE + 1.0471976f, 1400.0f);
    enmCreate("BossCard8_et", 0.0f, 0.0f, 100, 0, 0);
    circlePos(%F0, %F1, %ANGLE + 0.5235988f, 1400.0f);
    enmCreate("BossCard8_et", 0.0f, 0.0f, 100, 0, 0);
    circlePos(%F0, %F1, %ANGLE, 1400.0f);
    enmCreate("BossCard8_et", 0.0f, 0.0f, 100, 0, 0);
    circlePos(%F0, %F1, %ANGLE - 0.5235988f, 1400.0f);
    enmCreate("BossCard8_et", 0.0f, 0.0f, 100, 0, 0);
    circlePos(%F0, %F1, %ANGLE - 1.0471976f, 1400.0f);
    enmCreate("BossCard8_et", 0.0f, 0.0f, 100, 0, 0);
    return;
}[/code]

Float variable ``ANGLE`` is given the value ``%ANGLE_PLAYER - 3.1415927f``, where ``ANGLE_PLAYER`` is the angle between the parent object (in this case Momoyo) and the player.
``ANGLE_PLAYER`` is calculated as follows: ``ANGLE_PLAYER = arctan2(BOSS_Y - PLAYER_Y, BOSS_X - PLAYER_X)``

Instruction ``circlePosfloat(float varX, float varY, float ang, float radius)`` performs the following two actions: ``varX = cos(ang) * radius`` and ``varY = sin(ang) * radius``
[hl2]Instruction[/hl2] ``circlePos(%F0, %F1, %ANGLE + 1.0471976f, 1400.0f)`` performs the following two actions: ``F0 = cos(ANGLE + 1.0471976f) * 1400.0`` and ``F1 = sin(ANGLE + 1.0471976f) * 1400.0``

Instruction ``enmCreate`` is quite complicated, so it will be summarised in this page. To put it simply, ``enmCreate("BossCard8_et", 0.0f, 0.0f, 100, 0, 0);`` creates an enemy ``BossCard8_et`` at Momoyo's position.

The ``return`` keyword tells Momoyo to go back to the subroutine ``BossCard8`` where she will keep looping her attacking.

#### BossCard8_et
[hr_minor]

For convenience, the code is posted below again:

[code]void BossCard8_et() {
    var DECREMENT;
    [...]
    $DECREMENT = $I1;
BossCard8_et_1040:
    [...]
    etOn(1);
    wait(3);
BossCard8_et_1284:
    if ($DECREMENT--) goto BossCard8_et_1040 @ 0;
    delete();
}
[/code]

[hl2]Integer variable ``DECREMENT`` is declared and is assigned the value ``$I1``, which was declared in the subroutine ``BossCard8``[/hl2].
Instruction ``etOn(int id)`` makes the bullets spawn. In this case, ``etOn(1)`` spawns the arrowhead bullets.
Instruction ``wait(int time);`` stops the subroutine execution for ``time`` frames, so ``wait(3)`` stops the execution time for 3 frames.
Instruction ``delete();`` deletes the caller once called. In this case, the subroutine ``BossCard8_et`` destroys itself once called.
Although it is written quite obtusely, the end part of subroutine ``BossCard8_et`` keeps shooting bullets through instruction ``etOn(1)`` until the integer variable ``DECREMENT`` is exactly equal to 0. The variable ``DECREMENT`` decreases by 1 every iteration.

### Bullet spam
[hr_minor]

Instruction ``floatTime(0, %F1, 3600, 0, 3.0f, 30.0f)`` writes to the variable ``F1`` for the next 3600 frames. However, [hl2]once those 3600 frames have passed, the variable ``F1`` is no longer updated by the instruction ``floatTime``[/hl2].
Instead, [hl2]the variable ``F1`` is set by the instruction ``circlePos``[/hl2], where ``F1 = sin(A - 1.0471976f) * 1400.0 = sin(ANGLE_PLAYER - 4.1887903f) * 1400.0`` Following this, the value ``I1`` is equal to ``F1`` but ``I1`` is an integer. The variable ``DECREMENT`` is equal to the value of ``I1``.

The invisible enemy bullet spawner ``BossCard8_et`` only despawns if ``DECREMENT`` is equal to 0. This means that the value ``DECREMENT`` must be low enough where the variable ``DECREMENT`` is in the range [0, 30] such that it doesn't go on infinitely (30 is chosen here because that is its intended maximum value that ZUN set according to ``floatTime``). For ``DECREMENT`` to be low enough, the value ``I1`` must be low enough. For ``I1`` to be low enough, the value of ``F1`` must be low enough. For ``F1`` to be low enough, ``sin(ANGLE_PLAYER -1.0471977f) * 1400.0`` must be low enough.

To give an estimate how precisely-small ``ANGLE_PLAYER`` for there to be no bullet cataclysm, the angles will be calculated for when there are a total of 0 and 30 lingering bullets.
For there to be exactly 0 lingering bullets, the variable ``ANGLE_PLAYER`` must be around [hl2]1.0471977[/hl2].
For there to be exactly 30 lingering bullets, the variable ``ANGLE_PLAYER`` must be around [hl2]1.0686279[/hl2].

If the variable ``ANGLE_PLAYER`` were any higher, then the integer variable ``DECREMENT`` is a [hl2]much higher value[/hl2]. This means that the invisible enemy bullet spawner would have to [hl2]shoot a couple thousand bullets[/hl2] before it despawns. Furthermore, if the variable ``ANGLE_PLAYER`` [hl2]were any lower[/hl2], then the integer variable ``DECREMENT`` is a negative value. It means that the value would have to underflow, meaning it has to [hl2]go through a couple billion iterations[/hl2] before the invisible enemy bullet spawner despawns.

To visualise how precise the angles are, [hl2]I have modified the stage such that Momoyo shoots lasers that are at the aforementioned angles[/hl2]. Anything outside the lasers gives the player waves that are going to last a while:

[img=bugs/th18/7_angle.png, hratio=2, other= ] [br]

In order to humanely timeout this spell, the player must be between the two lasers when Momoyo is spawning the invisible bullet spawning enemies. [hl2]This is humanely impossible to do[/hl2] considering Momoyo constantly moves around alongside the difficulty of the spell.

### Conclusion
[hr_minor]

[box=1000] The bullets on Dragon Eater [hl2]are shot a small number of times[/hl2] before it stops shooting. However, after 60 seconds there is an [hl2]overwhelming number of bullets[/hl2] each wave. This is because part of the [hl2]code is accessed differently[/hl2]. Instead, the amount of bullets that are shot depends on your [hl2]player angle[/hl2]. [/box]

[hr_major]
## [links]
[hr_minor]
### [rpy]
[hr_minor]

Soon

### [vid]
[hr_minor]

Soon
